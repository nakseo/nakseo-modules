buildscript {
    ext {
        springBootVersion = "1.5.10.RELEASE"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"


    /*
     * # mavenLocal() 설정
     *  @ eclipse 설정시 주의사항
     * 		- maven 설정에서 user settings를 통해 local repository를 변경해도 gradle에서는 기본 repository 경로를 참조하는듯 하다.
     *  	- 기본 repository 경로인 'C:/Users/{username}/.m2/repository' 하위에 관련 .jar 파일을 설정요망.
     *		- C:/Users/{username}/.m2/repository/{group}/{name}/{version}/{name}-{version}.jar
     */
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:4+"
        testCompile("org.springframework.boot:spring-boot-starter-test")
        compile('org.springframework.boot:spring-boot-starter-web')
    }
}

project(':library') {
	bootRepackage {
	    enabled = false
	}
	
	dependencies {
        compile('org.projectlombok:lombok:1.16.6')
        compile('org.springframework.boot:spring-boot-starter-jdbc')
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.43'
        compile group: 'log4sql', name: 'log4sql', version: '7.8'
	}
}

project(':web') {
	bootRepackage {
	    mainClass = 'kr.co.nakseo.WebApplication'
	}
	
	dependencies {
	    compile project(':library')
	    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
	    //compile group: 'de.neuland-bfi', name: 'spring-jade4j', version: '1.2.7'
	}
}

project('api') {
	bootRepackage {
	    mainClass = 'kr.co.nakseo.ApiApplication'
	}
	
	dependencies {
	    compile project(':library')
	}
}